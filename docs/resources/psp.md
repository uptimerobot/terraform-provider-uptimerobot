---
page_title: "uptimerobot_psp Resource - uptimerobot"
subcategory: ""
description: |-
  Manages an UptimeRobot Public Status Page (PSP).
---

# uptimerobot_psp (Resource)

Manages an UptimeRobot Public Status Page (PSP).

## Example Usage

### Basic Public Status Page

```terraform
resource "uptimerobot_monitor" "website" {
  name     = "Website"
  type     = "HTTP"
  url      = "https://example.com"
  interval = 300
  timeout  = 30
}

resource "uptimerobot_monitor" "api" {
  name     = "API"
  type     = "HTTP"
  url      = "https://api.example.com"
  interval = 300
  timeout  = 30
}

resource "uptimerobot_psp" "public_status" {
  name = "Example.com Status"
  monitor_ids = [
    uptimerobot_monitor.website.id,
    uptimerobot_monitor.api.id,
  ]
}
```

### Status Page with Custom Domain

```terraform
resource "uptimerobot_monitor" "website" {
  name     = "Website"
  type     = "HTTP"
  url      = "https://example.com"
  interval = 300
  timeout  = 30
}

resource "uptimerobot_psp" "custom_domain_status" {
  name          = "Example.com Status"
  custom_domain = "status.example.com"
  monitor_ids = [
    uptimerobot_monitor.website.id,
  ]

  # SEO settings
  no_index = false

  # Hide direct URLs for security
  hide_url_links = true
}
```

### Password Protected Status Page

```terraform
resource "uptimerobot_monitor" "internal_api" {
  name     = "Internal API"
  type     = "HTTP"
  url      = "https://internal-api.example.com"
  interval = 300
  timeout  = 30
}

resource "uptimerobot_psp" "internal_status" {
  name = "Internal Services Status"
  monitor_ids = [
    uptimerobot_monitor.internal_api.id,
  ]

  # Password protection for internal use
  custom_settings = {
    features = {
      show_monitor_url = false # Hide URLs
    }
  }

  # Prevent search engine indexing
  no_index = true

  # Hide URL links for security
  hide_url_links = true
}
```

### Status Page with Custom Styling

```terraform
resource "uptimerobot_monitor" "website" {
  name     = "Website"
  type     = "HTTP"
  url      = "https://example.com"
  interval = 300
  timeout  = 30
}

resource "uptimerobot_psp" "branded_status" {
  name = "Example.com Status"
  monitor_ids = [
    uptimerobot_monitor.website.id,
  ]

  # Custom branding files (multipart upload)
  logo_file_path = "${path.module}/assets/logo.png"
  icon_file_path = "${path.module}/assets/icon.png"
  # Custom styling
  custom_settings = {
    colors = {
      main = "#4CAF50"
      text = "#333333"
      link = "#2196F3"
    }

    font = {
      family = "Arial, sans-serif"
    }

    page = {
      theme   = "light"
      layout  = "logo_on_left"
      density = "normal"
    }

    features = {
      show_uptime_percentage = true
      show_overall_uptime    = true
      show_bars              = true
      enable_floating_status = true
    }
  }

  # Google Analytics
  ga_code = "G-ABCDEFGH12"

  # Cookie consent
  show_cookie_bar                = true
  use_small_cookie_consent_modal = true
}
```

`logo_file_path` and `icon_file_path` accept only local filesystem paths.
If you need to fetch files from a URL, download them first (for example in CI) and point Terraform to the downloaded file paths.

## Status Page Features

- **Custom Domain**: Use your own domain for the status page
- **Password Protection**: Restrict access with a password
- **Custom Styling**: Apply custom CSS and branding
- **Google Analytics**: Track status page visitors
- **SEO Control**: Control search engine indexing

## Monitor Selection

You can include specific monitors in your status page by providing their IDs in the `monitor_ids` list. This allows you to:
- Show only public-facing services
- Create different status pages for different audiences
- Group related services together

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) Name of the PSP

### Optional

- `custom_domain` (String) Custom domain for the PSP
- `custom_settings` (Attributes) Custom settings for the PSP (see [below for nested schema](#nestedatt--custom_settings))
- `ga_code` (String) Google Analytics code
- `hide_url_links` (Boolean) Whether to hide URL links
- `icon` (String) PSP icon URL returned by API (read-only). Set to empty string to clear existing icon.
- `icon_file_path` (String) Local filesystem path to icon image file to upload via multipart/form-data.
- `logo` (String) PSP logo URL returned by API (read-only). Set to empty string to clear existing logo.
- `logo_file_path` (String) Local filesystem path to logo image file to upload via multipart/form-data.
- `monitor_ids` (Set of Number) Set of monitor IDs
- `no_index` (Boolean) Whether to prevent indexing
- `password` (String, Sensitive) Password for accessing the PSP page.
- Redacted in CLI output and logs.
- Not returned by the UptimeRobot API. 'is_password_set' attribute tells that password was set for psp or not.
- The provider keeps the last configured value in state.
- `pinned_announcement_id` (Number) ID of pinned announcement
- `share_analytics_consent` (Boolean) Whether analytics sharing is consented
- `show_cookie_bar` (Boolean) Whether to show cookie bar
- `status` (String) Status of the PSP
- `use_small_cookie_consent_modal` (Boolean) Whether to use small cookie consent modal

### Read-Only

- `homepage_link` (String) Homepage link for the PSP
- `id` (String) PSP identifier
- `is_password_set` (Boolean) Whether a password is set for the PSP
- `monitors_count` (Number) Number of monitors in the PSP
- `subscription` (Boolean) Whether subscription is enabled
- `url_key` (String) URL key for the PSP

<a id="nestedatt--custom_settings"></a>
### Nested Schema for `custom_settings`

Optional:

- `colors` (Attributes) Color settings (see [below for nested schema](#nestedatt--custom_settings--colors))
- `features` (Attributes) Feature settings (see [below for nested schema](#nestedatt--custom_settings--features))
- `font` (Attributes) Font settings (see [below for nested schema](#nestedatt--custom_settings--font))
- `page` (Attributes) Page settings (see [below for nested schema](#nestedatt--custom_settings--page))

<a id="nestedatt--custom_settings--colors"></a>
### Nested Schema for `custom_settings.colors`

Optional:

- `link` (String) Link color
- `main` (String) Main color
- `text` (String) Text color


<a id="nestedatt--custom_settings--features"></a>
### Nested Schema for `custom_settings.features`

Optional:

- `enable_details_page` (Boolean) Whether to enable details page
- `enable_floating_status` (Boolean) Whether to enable floating status
- `hide_paused_monitors` (Boolean) Whether to hide paused monitors
- `show_bars` (Boolean) Whether to show bars
- `show_monitor_url` (Boolean) Whether to show monitor URL
- `show_outage_details` (Boolean) Whether to show outage details
- `show_outage_updates` (Boolean) Whether to show outage updates
- `show_overall_uptime` (Boolean) Whether to show overall uptime
- `show_uptime_percentage` (Boolean) Whether to show uptime percentage


<a id="nestedatt--custom_settings--font"></a>
### Nested Schema for `custom_settings.font`

Optional:

- `family` (String) Font family


<a id="nestedatt--custom_settings--page"></a>
### Nested Schema for `custom_settings.page`

Optional:

- `density` (String) Page density
- `layout` (String) Page layout
- `theme` (String) Page theme
